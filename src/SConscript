# (c) 2008-2011 by Anton Korenyushkin

import subprocess

Import("env")

env.Command('init.js.h', 'init.js', '\
(echo "namespace\\n{\\nconst char INIT_JS[] = {";\
 xxd -i < $SOURCES;\
 echo "};\\n}";\
) > $TARGET')

db_sources = [
    'db.cc',
    'parser.cc',
    'translator.cc',
    'common.cc',
    'debug.cc',
    ]

js_sources = [
    'js.cc',
    'js-common.cc',
    'js-core.cc',
    'js-db.cc',
    'js-fs.cc',
    'js-binary.cc',
    'js-proxy.cc',
    'js-script.cc',
    'js-socket.cc',
    'js-http-parser.cc',
    'js-git.cc',
    ]

db_objects, js_objects = [
    [env.Object(source) for source in sources]
    for sources in (db_sources, js_sources)]

revision = subprocess.Popen(
    ['git', 'log', '-n1', '--date=short', '--pretty=format:%cd %h', 'HEAD'],
    stdout=subprocess.PIPE).stdout.read()[:-1]

js_objects.append(env.Object(
    'main.cc', CCFLAGS=env['CCFLAGS'] + ['-DREVISION=\\"%s\\"' % revision]))

Return('db_objects', 'js_objects')
